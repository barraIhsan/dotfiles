# ---------------------------------------------------------------------

# Import current env to systemd env (global ig) once
# will touch the file at the very last
importenv() {
  flag=$XDG_RUNTIME_DIR/zsh_envimport
  [[ ! -f $flag ]] && systemctl --user import-environment "$@"
}

# ---------------------------------------------------------------------

# XDG Base Directory Specification
export XDG_CONFIG_HOME=$HOME/.config
export XDG_CACHE_HOME=$HOME/.cache
export XDG_DATA_HOME=$HOME/.local/share
export XDG_STATE_HOME=$HOME/.local/state
importenv XDG_CONFIG_HOME XDG_CACHE_HOME XDG_DATA_HOME XDG_STATE_HOME

# XDG User Directory Specification
eval $(grep -E '^XDG_.+_DIR=' $XDG_CONFIG_HOME/user-dirs.dirs | sed 's/^/export /')
importenv $(grep -oE '^XDG_.+_DIR' $XDG_CONFIG_HOME/user-dirs.dirs | tr '\n' ' ')

# ---------------------------------------------------------------------

# Preserve History
HISTFILE=$XDG_STATE_HOME/zsh_history
HISTSIZE=1000000
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory          # Append to $HISTFILE, not override
setopt sharehistory           # Sync across session
setopt hist_ignore_space      # Ignore commands starting with a space
setopt hist_ignore_all_dups   # Remove all duplicates
setopt hist_find_no_dups      # Avoid showing duplicates in Ctrl+R
setopt hist_expire_dups_first # Expire the oldest duplicate first

# Completion & Keybind
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'eza -1 --color=always $realpath'
bindkey -e
bindkey '^p' history-search-backward
bindkey '^n' history-search-forward
autoload -Uz compinit && compinit -d $XDG_CACHE_HOME/zcompdump

# Edit the current command line in $EDITOR
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^v' edit-command-line

# ---------------------------------------------------------------------

# Run fastfetch
fastfetch

# Initialize programs
eval "$(starship init zsh)"
eval "$(zoxide init --cmd cd zsh)"
eval "$(fzf --zsh)"

# Aliases
alias c="clear"
alias vim="nvim"
alias v="nvim"
alias ls="eza --group-directories-first --icons=auto --git -h"
alias ll="ls -la"
alias cat="bat"
alias lg="lazygit"
alias youtube-dl="yt-dlp"
# Preserve extended attribute
alias cp="cp -a"
alias rsync="rsync -X"

# Yazi shell wrapper
function y() {
	local tmp="$(mktemp -t "yazi-cwd.XXXXXX")" cwd
	yazi "$@" --cwd-file="$tmp"
	if cwd="$(command cat -- "$tmp")" && [ -n "$cwd" ] && [ "$cwd" != "$PWD" ]; then
		builtin cd -- "$cwd"
	fi
	rm -f -- "$tmp"
}

# Source nvm
export NVM_DIR=$XDG_DATA_HOME/nvm
source $NVM_DIR/nvm.sh

# Start ssh-agent (if it's not started yet), and remove any output
[ -z "$SSH_AUTH_SOCK" ] && eval "$(ssh-agent -s | grep -v '^echo')"

# Reduce CUDA memory fragmentation in PyTorch by
# enabling memory segments
export PYTORCH_CUDA_ALLOC_CONF=expandable_segments:True
importenv PYTORCH_CUDA_ALLOC_CONF

# ---------------------------------------------------------------------

# clean home dir
# https://wiki.archlinux.org/title/XDG_Base_Directory
# config
export NPM_CONFIG_USERCONFIG=$XDG_CONFIG_HOME/npm/npmrc
export GTK2_RC_FILES=$XDG_CONFIG_HOME/gtk-2.0/gtkrc
export _JAVA_OPTIONS=-Djava.util.prefs.userRoot=$XDG_CONFIG_HOME/java
importenv NPM_CONFIG_USERCONFIG GTK2_RC_FILES _JAVA_OPTIONS
# data
export GNUPGHOME=$XDG_DATA_HOME/gnupg
export CARGO_HOME=$XDG_DATA_HOME/cargo
export RUSTUP_HOME=$XDG_DATA_HOME/rustup
export GRADLE_USER_HOME=$XDG_DATA_HOME/gradle
export FVM_CACHE_PATH=$XDG_DATA_HOME/fvm
export NODE_REPL_HISTORY=$XDG_DATA_HOME/node_repl_history
importenv GNUPGHOME CARGO_HOME RUSTUP_HOME GRADLE_USER_HOME FVM_CACHE_PATH NODE_REPL_HISTORY
# state
export PYTHON_HISTORY=$XDG_STATE_HOME/python_history
importenv PYTHON_HISTORY

# use tty gpg
export GPG_TTY=$(tty)
importenv GPG_TTY

# set default editor and manpager to neovim
export VISUAL=nvim
export EDITOR=$VISUAL
export MANPAGER="nvim +Man!"
importenv VISUAL EDITOR MANPAGER

# Add directory to PATH env
typeset -U path PATH # prevent duplicate
path+=~/.local/bin
path+=$CARGO_HOME/bin
path+=$FVM_CACHE_PATH/default/bin
importenv PATH

# Lastly, touch that file
touch $flag

# ---------------------------------------------------------------------

# Initialize Zinit
ZINIT_HOME="$XDG_DATA_HOME/zinit/zinit.git"

# Download Zinit, if it's not there yet
if [ ! -d "$ZINIT_HOME" ]; then
  mkdir -p "$(dirname $ZINIT_HOME)"
  git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
fi

# Source / Load zinit
source $ZINIT_HOME/zinit.zsh

# Add zsh plugin
zinit light zsh-users/zsh-syntax-highlighting
zinit light zsh-users/zsh-completions
zinit light zsh-users/zsh-autosuggestions
zinit light Aloxaf/fzf-tab
zinit snippet OMZP::command-not-found
zinit snippet OMZP::sudo

zinit cdreplay -q

# ---------------------------------------------------------------------
# vim: filetype=sh syntax=sh
